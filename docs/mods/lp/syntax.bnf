<stmt> ::= <for>
         | <if>
         | <fdef>
         | <struct>
	 | <ret>
         | <bind>
         | <logic>
         | <PASS>
         | <EOL>
         | <ERR>

<for> ::= <FOR> <forcond> <fbody>

<forcond> ::= <SYM> <IN> <range>
            | {<forlogic>}

<forlogic> ::= <logic> sepby <SEMI>

<struct> ::= <STRUCT> <SYM> <EQ> <fbody>

<if> ::= <IF> <logic> <fbody> [<else>]

<else> ::= <ELSE> (<if>|<fbody>)

<logic> ::= <eqcmp> (<AND>|<OR>) <logic>
          | <eqcmp>

<eqcmp> ::= <difcmp> (<EEQ>|<NEQ>) <eqcmp>
          | <difcmp>

<difcmp> ::= <expr> (<LSS>|<GTR>|<LEQ>|<GEQ>) <difcmp>
           | <expr>

<fdef> ::= <FN> <dot> {<SYM>} <EQ> <fbody>

<fbody> ::= <INDENT> {<stmt>} <DEDENT>
          | <logic>

<ret> :: = <RET> <logic>

# name: Type = something
<bind> ::= <typedsym> <bindsym> <logic>
         | <typedsym>
         | <SYM> <bindsym> <logic>

<bindsym> ::= <EQ> | <AEQ> | <SEQ> | <MEQ> | <DEQ> | <DDEQ> | <MOQ>

# "name: Type" or "name"
<typedsym> ::= <dot> <COLN> <typeanno>

# "i64" or "Module.Type[5]" or "[pointer]" or "[ptr][10][10]"
<typeanno> ::= <typeanno> <ptr>
             | <ptr>
             | <dot>

# maths
<expr> ::= <term> {(<ADD>|<SUB>) <term>}
<term> ::= <fact> {(<MUL>|<DIV>|<DDIV>|<MOD>) <fact>}
<fact> ::= <LPAR> <logic> <RPAR>
         | <cast>

# something as OtherType
<cast> ::= <not> <AS> <typeanno>
         | <not>

# "!something" or "not something"
<not> ::= <NOT> <lit>
        | <lit>

# constant literals
<lit> ::= <call>
        | <structinit>
        | <arg>

<structinit> ::= <DOT> <LCRL> {<typedsym> sepby <COMA>} <RCRL>

# func_name arg1 arg2 ...
<call> ::= <dot> [{<arg>}]
<arg> ::= <LPAR> <logic> <RPAR>
        | <range>
        | <num>
        | <STR>
        | <CHR>
        | <ptr>
        | <arr>
        | <dot>

# X to Y
<range> ::= <num> <TO> <num>

# (-)value
<num> ::= <SUB> <num>
        | <INT>
        | <FLT>

# [expr]
<ptr> ::= <LBRC> <logic> <RBRC>

# { logic1, logic2, ... }
<arr> ::= <LCRL> {<logic> sepby <COMA>} <RCLR>

# "type.field" or "type"
<dot> ::= <SYM> <DOT> <dot>
        | <SYM>
