<stmt> ::= <import>
         | <for>
         | <if>
         | <fdef>
         | <struct>
         | <ret>
         | <bind>
         | <logic>
         | <PASS>
         | <EOL>
         | <ERR>

<import> ::= <IMPORT> <SYM>

<for> ::= <FOR> <forcond> <fbody>

<forcond> ::= <SYM> <IN> <range>
            | {<forlogic>}

<forlogic> ::= <pipe> sepby <SEMI>

<struct> ::= <STRUCT> <SYM> <EQ> <fbody>

<if> ::= <IF> <pipe> <fbody> [<else>]

<else> ::= <ELSE> (<if>|<fbody>)

<pipe> ::= <logic> <PIPE> <pipe>
         | <logic>

<logic> ::= <eqcmp> (<AND>|<OR>) <logic>
          | <eqcmp>

<eqcmp> ::= <difcmp> (<EEQ>|<NEQ>) <eqcmp>
          | <difcmp>

<difcmp> ::= <expr> (<LSS>|<GTR>|<LEQ>|<GEQ>) <difcmp>
           | <expr>

<fdef> ::= <FN> <dot> {<SYM>} <EQ> <fbody>

<fbody> ::= <INDENT> {<stmt>} <DEDENT>
          | <pipe>

<ret> :: = <RET> <pipe>

# name: Type = something
<bind> ::= <typedsym> <bindsym> <pipe>
         | <typedsym>
         | <SYM> <bindsym> <pipe>

<bindsym> ::= <EQ> | <AEQ> | <SEQ> | <MEQ> | <DEQ> | <DDEQ> | <MOQ>

# "name: Type" or "name"
<typedsym> ::= <dot> <COLN> <typeanno>

# "i64" or "Module.Type[5]" or "[pointer]" or "[ptr][10][10]"
<typeanno> ::= <typeanno> <deref>
             | <deref>
             | <dot>

# maths
<expr> ::= <term> {(<ADD>|<SUB>) <term>}
<term> ::= <fact> {(<MUL>|<DIV>|<DDIV>|<MOD>) <fact>}
<fact> ::= <LPAR> <pipe> <RPAR>
         | <cast>

# something as OtherType
<cast> ::= <neg> <AS> <typeanno>
         | <neg>

# negate
<neg> ::= "-" <not>
        | <not>

# "!something" or "not something"
<not> ::= <NOT> <lit>
        | <lit>

# constant literals
<lit> ::= <call>
        | <structinit>
        | <arg>

<structinit> ::= <DOT> <LCRL> <structfield> <RCRL>
<structbody> ::= {<structfield> sepby <COMMA>}
<structfield> ::= <SYM> <COLN> <pipe>

# func_name arg1 arg2 ...
<call> ::= <dot> [{<arg>}]
<arg> ::= <LPAR> <pipe> <RPAR>
        | <range>
        | <INT>
        | <FLT>
        | <STR>
        | <CHR>
        | <deref>
        | <arr>
        | <dot>

# X to Y
<range> ::= <INT> <TO> <INT>

# [expr]
<deref> ::= <LBRC> <pipe> <RBRC>

# { pipe1, pipe2, ... }
<arr> ::= <LCRL> {<pipe> sepby ","} <RCLR>

# "type.field" or "type"
<dot> ::= <SYM> "." <dot>
        | <SYM>
